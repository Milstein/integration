*** Settings ***
Documentation     OF1.3 Suite for flow actions
...               - output ALL
...               - output CONTROLLER
...               - output TABLE
...               - output INPORT
...               - output LOCAL
...               - output NORMAL
...               - output FLOOD
...               - output ANY
...
...               NOTE: for OVS, INPORT does not appear to be supported
Suite Setup       Create Session    session    http://${CONTROLLER}:${RESTCONFPORT}    auth=${AUTH}    headers=${HEADERS_XML}
Suite Teardown    Delete All Sessions
Test Template     Create And Remove Flow
Library           SSHLibrary
Library           Collections
Library           OperatingSystem
Library           String
Library           XML
Resource          ../../../libraries/FlowLib.txt
Library           ../../../libraries/RequestsLibrary.py
Library           ../../../libraries/Common.py
Variables         ../../../variables/Variables.py

*** Variables ***
${REST_CON}       /restconf/config/opendaylight-inventory:nodes
${REST_OPR}       /restconf/operational/opendaylight-inventory:nodes
${ipv4_src}       11.3.0.0/16
${ipv4_dst}       99.0.0.0/8
${eth_type}       0x800
${eth_src}        00:ab:cd:ef:01:23
${eth_dst}        ff:ff:ff:ff:ff:ff
${node_id}        openflow:1
##documentation strings
${INPORT_doc}     OF1.3: OFPP_INPORT = 0xfffffff8, /* Send the packet out the input port. This\nreserved port must be explicitly used\nin order to send back out of the input\nport. */\n
${TABLE_doc}      OF1.3: OFPP_TABLE = 0xfffffff9, /* Submit the packet to the first flow table NB: This destination port can only be used in packet-out messages. */
${NORMAL_doc}     OF1.3 OFPP_NORMAL = 0xfffffffa, /* Process with normal L2/L3 switching. */
${FLOOD_doc}      OF1.3 OFPP_FLOOD = 0xfffffffb, /* All physical ports in VLAN, except input port and those blocked or link down. */
${ALL_doc}        OF1.3: OFPP_ALL = 0xfffffffc, /* All physical ports except input port. */
${CONTROLLER_doc}    OF1.3 OFPP_CONTROLLER = 0xfffffffd, /* Send to controller. */
${LOCAL_doc}      OF1.3 OFPP_LOCAL = 0xfffffffe, /* Local openflow "port". */
${ANY_doc}        OF1.3 OFPP_ANY = 0xffffffff /* Wildcard port used only for flow mod (delete) and flow stats requests. Selects all flows regardless of output port (including flows with no output port). */

*** Test Cases ***    output port        tableID              flowID    verify OVS?    OVS specific strings
INPORT                [Documentation]    ${INPORT_doc}
                      [Tags]             inport
                      ${TEST_NAME}       22                   161       yes            actions=IN_PORT

TABLE                 [Documentation]    ${TABLE_doc}
                      [Tags]             table
                      ${TEST_NAME}       29                   261       yes            actions=${TEST_NAME}

NORMAL                [Documentation]    ${NORMAL_doc}
                      [Tags]             normal
                      ${TEST_NAME}       13                   361       yes            actions=${TEST_NAME}

FLOOD                 [Documentation]    ${FLOOD_doc}
                      [Tags]             flood
                      ${TEST_NAME}       47                   81        yes            actions=${TEST_NAME}

ALL                   [Documentation]    ${ALL_doc}
                      [Tags]             all
                      ${TEST_NAME}       42                   88        yes            actions=${TEST_NAME}

CONTROLLER            [Documentation]    ${CONTROLLER_doc}
                      [Tags]             controller
                      ${TEST_NAME}       81                   21        yes            actions=${TEST_NAME}

LOCAL                 [Documentation]    ${LOCAL_doc}
                      [Tags]             local
                      ${TEST_NAME}       122                  32        yes            actions=${TEST_NAME}

ANY                   [Documentation]    ${ANY_doc}
                      [Tags]             any
                      ${TEST_NAME}       222                  111       yes            actions=${TEST_NAME}

*** Keywords ***
Create And Remove Flow
    [Arguments]    ${output_port}    ${table_id}    ${flow_id}    ${verify_switch_flag}    ${additional_ovs_flowelements}
    Remove Default Flows    ${node_id}
    @{OVS_FLOWELEMENTS}    Create List    dl_dst=${eth_dst}    table=${table_id}    dl_src=${eth_src}    nw_src=${ipv4_src}    nw_dst=${ipv4_dst}
    ...    ${additional_ovs_flowelements}
    ##The dictionaries here will be used to populate the match and action elements of the flow mod
    ${ethernet_match_dict}=    Create Dictionary    type=${eth_type}    destination=${eth_dst}    source=${eth_src}
    ${ipv4_match_dict}=    Create Dictionary    source=${ipv4_src}    destination=${ipv4_dst}
    ##flow is a python Object to build flow details, including the xml format to send to controller
    ${flow}=    Create Inventory Flow
    Set "${flow}" "table_id" With "${table_id}"
    Set "${flow}" "id" With "${flow_id}"
    Clear Flow Actions    ${flow}
    Set Flow Output Action    ${flow}    0    0    ${output_port}
    Set Flow Ethernet Match    ${flow}    ${ethernet_match_dict}
    Set Flow IPv4 Match    ${flow}    ${ipv4_match_dict}
    Log    Flow XML is ${flow.xml}
    Add Flow To Controller And Verify    ${flow.xml}    ${node_id}    ${flow.table_id}    ${flow.id}
    Run Keyword If    "${verify_switch_flag}" == "yes"    Verify Flow On Mininet Switch    ${OVS_FLOWELEMENTS}
    Remove Flow From Controller And Verify    ${flow.xml}    ${node_id}    ${flow.table_id}    ${flow.id}
    Run Keyword If    "${verify_switch_flag}" == "yes"    Verify Flow Does Not Exist On Mininet Switch    ${OVS_FLOWELEMENTS}
