*** Settings ***
Documentation     Test suite for Topology Manager
Suite Teardown    Delete All Sessions
Library           Collections
Library           ../../libraries/RequestsLibrary.py
Library           ../../libraries/Common.py
Variables         ../../variables/Variables.py

*** Variables ***
${nodeconn1}   NodeConnector":{"type":"OF","node":{"type":"OF","id":"00:00:00:00:00:00:00:01"},"id":"1"}
${nodeconn2}   NodeConnector":{"type":"OF","node":{"type":"OF","id":"00:00:00:00:00:00:00:01"},"id":"2"}
${nodeconn3}   NodeConnector":{"type":"OF","node":{"type":"OF","id":"00:00:00:00:00:00:00:02"},"id":"3"}
${nodeconn4}   NodeConnector":{"type":"OF","node":{"type":"OF","id":"00:00:00:00:00:00:00:03"},"id":"3"}
${name}           test_userlink1
${key}            userLinks
${REST_CONTEXT}    /controller/nb/v2/topology

*** Test Cases ***
Get Topology
    [Documentation]    Get Topology and validate the result.
    [Tags]    get
    ${headers}    Create Dictionary    Content-Type    application/json
    Create Session    session    http://${CONTROLLER}:8080   headers=${headers}    auth=${auth}
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}
    Should Be Equal As Strings    ${resp.status_code}    200    Response    status code error
    Log    ${resp.content}
    Should Contain X Times  ${resp.content}   ${nodeconn1}   2
    Should Contain X Times  ${resp.content}   ${nodeconn2}   2
    Should Contain X Times  ${resp.content}   ${nodeconn3}   2
    Should Contain X Times  ${resp.content}   ${nodeconn4}   2
Add a userlink
    [Documentation]    Add a userlink, list to validate the result.
    [Tags]    add
    ${body}    Create Dictionary    name    ${name}    status    Success    srcNodeConnector
    ...    OF|1@OF|00:00:00:00:00:00:00:02    dstNodeConnector    OF|1@OF|00:00:00:00:00:00:00:03
    ${headers}    Create Dictionary    Content-Type    application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Put    session    ${REST_CONTEXT}/${CONTAINER}/userLink/${name}    data=${body}
    Should Be Equal As Strings    ${resp.status_code}    201    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/userLinks
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Contain Value    ${content}    ${body}

Remove a userlink
    [Documentation]    Remove a userlink, list to validate the result.
    [Tags]    remove
    ${body}    Create Dictionary    name    ${name}    status    Success    srcNodeConnector
    ...    OF|1@OF|00:00:00:00:00:00:00:02    dstNodeConnector    OF|1@OF|00:00:00:00:00:00:00:03
    ${headers}    Create Dictionary    Content-Type    application/json
    Create Session    session    http://${CONTROLLER}:8080    headers=${headers}    auth=${auth}
    ${resp}    Delete    session    ${REST_CONTEXT}/${CONTAINER}/userLink/${name}
    Should Be Equal As Strings    ${resp.status_code}    204    Response status code error
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}/userLinks
    Should Be Equal As Strings    ${resp.status_code}    200    Response status code error
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Not Contain Value    ${content}    ${body}
