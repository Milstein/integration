*** Settings ***
Documentation     Test suite for Topology Manager
Suite Setup       Create Session   session   http://${CONTROLLER}:8080   auth=${AUTH}   headers=${HEADERS}
Suite Teardown    Delete All Sessions
Library           Collections
Library           ../../../libraries/RequestsLibrary.py
Library           ../../../libraries/Common.py
Library           ../../../libraries/Topologynew.py
Variables         ../../../variables/Variables.py

*** Variables ***
${nodeprefix}   openflow:
${REST_CONTEXT}    /controller/nb/v2/topology

*** Test Cases ***
Get Topology
    [Documentation]    Get Topology and validate the result.
    [Tags]    get
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}
    Should Be Equal As Strings    ${resp.status_code}    200 
    Log    ${resp.content}
    ${TOPO_TREE_DEPTH}    Convert To Integer  ${TOPO_TREE_DEPTH}
    ${TOPO_TREE_FANOUT}   Convert To Integer  ${TOPO_TREE_FANOUT}
    ${numnodes}     Num Of Nodes    ${TOPO_TREE_DEPTH}    ${TOPO_TREE_FANOUT}
    Should Contain X Times  ${resp.content}   "${nodeprefix}1"   ${TOPO_TREE_FANOUT*2}
    ${leaflist}    Get Ids Of Leaf Nodes    ${TOPO_TREE_FANOUT}   ${TOPO_TREE_DEPTH} 
    :FOR    ${IND}  IN RANGE    2   ${numnodes+1}
    \   ${linkcnt}      Num Of Links For Node       ${IND}      ${leaflist}     ${TOPO_TREE_FANOUT}
    \   Should Contain X Times  ${resp.content}     "${nodeprefix}${IND}"    ${linkcnt*2}   


