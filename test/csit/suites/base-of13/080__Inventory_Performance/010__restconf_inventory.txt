*** Settings ***
Documentation     Test suite for RESTCONF inventory
Suite Setup       Create Session   session   http://${CONTROLLER}:${PORT}   auth=${AUTH}   headers=${HEADERS_XML}
Suite Teardown    Delete All Sessions
Library           Collections
Library           ../../../libraries/RequestsLibrary.py
Library           ../../../libraries/Common.py
Variables         ../../../variables/Variables.py

*** Variables ***
${REST_CONTEXT}    /restconf/operational/opendaylight-inventory:nodes


*** Test Cases *** 
Get list of nodes
    [Documentation]    Get the inventory
	${resp}    Get    session    ${REST_CONTEXT}
    Should Be Equal As Strings    ${resp.status_code}    200
	${TOPO_TREE_DEPTH}    Convert To Integer  ${TOPO_TREE_DEPTH}
   	${TOPO_TREE_FANOUT}   Convert To Integer  ${TOPO_TREE_FANOUT}
	${numnodes}		Num Of Nodes	${TOPO_TREE_DEPTH}	${TOPO_TREE_FANOUT}
	:FOR	${IND}	IN RANGE	1	${numnodes+1}	
    \   Should Contain     ${resp.content}  openflow:${IND}

Get nodeconnector for the root node 
    [Documentation]    Get the inventory for the root node
    ${TOPO_TREE_FANOUT}   Convert To Integer  ${TOPO_TREE_FANOUT}
    ${resp}    Get    session    ${REST_CONTEXT}/node/openflow:1
    Should Be Equal As Strings   ${resp.status_code}    200
    Check conn loop   ${TOPO_TREE_FANOUT}   1  ${resp.content}

Get nodeconnector for a node 
    [Documentation]    Get the inventory for a node
    ${TOPO_TREE_DEPTH}    Convert To Integer  ${TOPO_TREE_DEPTH}
    ${TOPO_TREE_FANOUT}   Convert To Integer  ${TOPO_TREE_FANOUT}
    ${numnodes}     Num Of Nodes    ${TOPO_TREE_DEPTH}    ${TOPO_TREE_FANOUT}
    :FOR    ${IND}  IN RANGE    2   ${numnodes+1}
    \   ${resp}    Get    session    ${REST_CONTEXT}/node/openflow:${IND}
    \   Should Be Equal As Strings   ${resp.status_code}    200
    \   Check conn loop   ${TOPO_TREE_FANOUT+1}   ${IND}  ${resp.content}

Get Stats for a node
    [Documentation]    Get the stats for a node
    ${TOPO_TREE_DEPTH}    Convert To Integer  ${TOPO_TREE_DEPTH}
    ${TOPO_TREE_FANOUT}   Convert To Integer  ${TOPO_TREE_FANOUT}
    ${numnodes}     Num Of Nodes    ${TOPO_TREE_DEPTH}    ${TOPO_TREE_FANOUT}
    :FOR    ${IND}  IN RANGE    1   ${numnodes+1}
    \   ${resp}    Get    session    ${REST_CONTEXT}/node/openflow:${IND}
    \   Should Be Equal As Strings   ${resp.status_code}    200
    \   Should Contain     ${resp.content}      opendaylight-port-statistics
    \   Should Contain     ${resp.content}      opendaylight-flow-table-statistics

*** Keywords ***
Check conn loop
    [Arguments]     ${arg}  ${outerind}     ${content}
    :FOR    ${var}  IN RANGE    1   ${arg+1}
    \   Should Contain     ${content}      openflow:${outerind}:${var}
