*** Settings ***
Documentation     Test suite for Flow Programmer
Suite Setup       Create Session   session   http://${CONTROLLER}:8080  auth=${AUTH}   headers=${HEADERS}
Suite Teardown    Delete All Sessions
Library           Collections
Library           ../../../libraries/RequestsLibrary.py
Library           ../../../libraries/Common.py
Variables         ../../../variables/Variables.py

*** Variables ***
${name}           flow1
${key}            flowConfig
${node_id}        00:00:00:00:00:00:00:02
${REST_CONTEXT}    /controller/nb/v2/flowprogrammer
${REST_CONTEXT_ST}    /controller/nb/v2/statistics
${FLOW}           "10.0.0.1"

*** Test Cases ***
Add a flow
    [Documentation]    Add a flow, list to validate the result.
    [Tags]    add
    ${node}    Create Dictionary    type    OF    id    ${node_id}
    ${actions}    Create List    OUTPUT=1
    ${body}    Create Dictionary    name    ${name}    installInHw    true    node
    ...    ${node}    priority    1    etherType    0x800    nwDst
    ...    10.0.0.1/32    actions    ${actions}
    ${resp}    Put    session    ${REST_CONTEXT}/${CONTAINER}/node/OF/${node_id}/staticFlow/${name}    data=${body}
    Should Be Equal As Strings    ${resp.status_code}    201
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}
    Should Be Equal As Strings    ${resp.status_code}    200
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Contain Value    ${content}    ${body}
Check flow in flow stats
    [Documentation]    Show flow stats and validate result
    [Tags]   get
    Sleep   30
    ${resp}    Get    session    ${REST_CONTEXT_ST}/${CONTAINER}/flow
    Should Be Equal As Strings    ${resp.status_code}    200 
    Log    ${resp.content}
    Should Contain    ${resp.content}    ${FLOW}
Remove a flow
    [Documentation]    Remove a flow, list to validate the result.
    [Tags]    remove
    ${node}    Create Dictionary    type    OF    id    ${node_id}
    ${actions}    Create List    OUTPUT=1
    ${body}    Create Dictionary    name    ${name}    installInHw    true    node
    ...    ${node}    priority    1    etherType    0x800    nwDst
    ...    10.0.0.1/32    actions    ${actions}
    ${resp}    Delete    session    ${REST_CONTEXT}/${CONTAINER}/node/OF/${node_id}/staticFlow/${name}
    Should Be Equal As Strings    ${resp.status_code}    204
    ${resp}    Get    session    ${REST_CONTEXT}/${CONTAINER}
    Should Be Equal As Strings    ${resp.status_code}    200
    ${result}    To JSON    ${resp.content}
    ${content}    Get From Dictionary    ${result}    ${key}
    List Should Not Contain Value    ${content}    ${body}
Check flow is not in flow stats
    [Documentation]    Show flow stats and validate result
    [Tags]    get
    Sleep   30
    ${resp}    Get    session    ${REST_CONTEXT_ST}/${CONTAINER}/flow
    Should Be Equal As Strings    ${resp.status_code}    200 
    Log    ${resp.content}
    Should Not Contain    ${resp.content}    ${FLOW}

